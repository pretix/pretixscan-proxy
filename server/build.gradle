plugins {
    id 'application'
    id 'kotlin'
    id 'kotlin-kapt'
    id 'com.palantir.git-version' version '0.12.3'
}
apply from: '../libpretixsync-repo/libpretixsync/versions.gradle'

group 'eu.pretix.pretixscan'
version gitVersion()

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // Server dependencies
    implementation "io.javalin:javalin:3.13.11"
    implementation "org.slf4j:slf4j-simple:1.7.32"
    implementation "org.postgresql:postgresql:42.3.8"
    implementation 'net.harawata:appdirs:1.2.1'

    // libpretixsync dependencies
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_core_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_kotlin_version"
    implementation "net.i2p.crypto:eddsa:$eddsa_version"
    implementation "com.google.protobuf:protobuf-javalite:$protobuf_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "io.requery:requery:$requery_version"
    implementation "io.requery:requery-kotlin:$requery_version"
    implementation "joda-time:joda-time:$joda_version"
    implementation "org.json:json:$json_version"
    implementation "net.sourceforge.streamsupport:streamsupport-cfuture:$cfuture_version"
    kapt "io.requery:requery-processor:$requery_version"
    kapt "javax.annotation:jsr250-api:$jsr250_version"
    implementation(project(':libpretixsync')) {
        transitive = false
    }

    // Test dependencies
    testImplementation project(path: ':libpretixsync')
    testImplementation(testFixtures(project(':libpretixsync'))) {
        transitive = false
    }
    testImplementation "org.xerial:sqlite-jdbc:$sqlite_jdbc_version"
    testImplementation "junit:junit:$junit_version"
}

mainClassName = "eu.pretix.pretixscan.scanproxy.Server"

jar {
    manifest {
        attributes "Main-Class": mainClassName
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
